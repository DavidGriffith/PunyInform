!-------------------------------------------------
! PART I: THE HOUSE ON THE OTHER SIDE OF THE STORM
!-------------------------------------------------

!-------------------------------------------------
! Locations
!-------------------------------------------------

!-------------------------------------------------
!----- The flying boat
!-------------------------------------------------

Object P1L_Flying_Boat "The flying boat"
	with
		description "The old boat that your father and grandfather used to sail the aether from one end to the other when they were young. It now shows its
            age, creaking threateningly against the wind of the nearby storm. The black clouds roar and turn, advancing at full speed towards your flying vessel.",
        cheap_scenery
            1 'mast' "The mast crackles incessantly, constantly threatening to break."
            1 'storm' "The storm draws closer and closer and threatens to engulf your small boat within its black clouds. From time to time,
                lightning illuminates its dark surface."
            1 'aether' "Aether is the substance that flows through the known universe, sustains the flight of floating islands and continents and
                allows navigation between them."
            'flying' 'boat' "The boat has a long and intense history compared to other, larger vessels. Thanks to it, your father was able to
                discover the Infinite River on the other side of the northern divide. Thanks to it, your grandfather found the ruins
                of Neuba Yorka, with its large, broken glass towers. And thanks to you, the boat's fate is now in the hands of the storm...",
        turns_active,
        daemon [;
            switch (++(self.turns_active)) {
                4: "^For a moment the clouds open up and in the distance you spot a small floating islet, sparsely covered in weatherworn vegetation.
                    You think you can discern the ruins of a building, perched on top of the island, just before it is swallowed up again by the fog.";
                6: "^Strong lightning illuminates the entire sky for a tenth of a second.";
                9: "^The storm is about to engulf your boat!";
                10: StopDaemon(self);
                    move P1O_Notebook to Limbo;
                    PlayerTo(P1L_Cliff);
                    print "^With a loud burst, lightning strikes your small boat, splitting it in two halves! Your body is thrown out, flying like a rag doll towards certain
                    death...^^^^You wake up dazed, your back aching but surprisingly intact. It seems that, instead of smashing against some floating rock or, even
                    worse, spending your days endless spinning through the infinite aether, you have had the immense luck of falling a short distance onto a soft
                    mattress of grass...^^";
                    <Look>;
            }
        ],
	has light;

Object -> P1O_Notebook "notebook"
	with
		name 'notebook',
        page 0,
        description [;
            print "Your old notebook has been with you for longer than you can remember. Its bruised cover wears the
                scratches and scars gained over the years with dignity.^";
            if (self.page < 3) self.page++;
            switch(self.page) {
                1: "You open the notebook at the last entry, the fruit of your research before beginning this journey:
                    ^^THE PHANTOM ZONE:
                    ^Although commonly referred to by this singular term, in reality there are many phantom zones, scattered in very diverse sections
                    of the aether. They are highly unstable areas, both climatologically and energetically. Despite the difficulty of life
                    developing on the few floating islands existing in the zones, numerous travellers have claimed to see ruins of human buildings.
                    ^It's not just these structures that give the areas the name ~Phantom Zone~, it's also the swarms of spectral beings,
                    commonly known as ~pale ones~, who seem attracted to them.
                    ^Many theorise that these zones are close to some of the cataclysms that occurred prior to the Great Transformation; the event
                    that changed the face of the known universe. Traveling around these places is discouraged due to their dangerous nature.
                    ^^The notes continue, but you find the words difficult to focus on, given how your life is in danger right now. You decide to
                    close the notebook.";
                2: "You open the notebook at one of the oldest entries. Written in adolescent handwriting, this research was a product of your concern at the time
                    about the changes you were experiencing and could not explain:
                    ^^EMPATHS:
                    ^Empaths can perceive emotions as a physical or spiritual current between bodies. It is said they
                    can sometimes contact the astral world and communicate with the dead, or perceive past situations that have left an
                    indelible mark on a particular place.
                    ^Empaths usually have a restless personality prone to violent emotional outbursts. That is why some scientists doubt their
                    existence, considering any symptoms a product of a kind of collective hysteria.
                    ^^You took even more notes on the subject, but you already know them by heart, and this is not the place or the best time to
                    consult them.";
                default: "There are many more entries in this notebook, but none of them are useful to you in this time of crisis.";
            }
        ],
        before [;
            Open: <<Examine self>>;
        ];

Object -> P1O_Sail "sail"
    with
        name 'sail',
        describe [;
            if (self has general) "Despite having the sail lowered, the force of the wind seems capable by itself of tearing the mast from your vessel...";
            "The sail hits the mast violently, and threatens to split it in two if it is not lowered...";
        ],
        description [;
            if (self has general) "The sail has been lowered. At least there is now one part of this boat that may survive the next few minutes.";
            "The hoisted sail, swollen with the winds of the storm, dangerously bashes against the mast. 
            Perhaps it would be advisable to lower it until the storm passes...";
        ],
        before [;
            Lower: if (self has general) "The sail is already lowered!";
                give self general;
                "You attempt to lower the sail, with extreme difficulty, fighting against the elements at every stage. You finally succeed, but 
                even with the sail down it seems that the situation has improved only marginally...";
            Raise: if (self hasnt general) "The sail is hoisted already!";
                "Now is not the time to hoist the sail! If you do, it's likely to split with the force of the wind...";
        ],
	has static;


!-------------------------------------------------
!----- The cliff
!-------------------------------------------------

Object P1L_Cliff "The cliff"
	with
		description "You find yourself on a cliff, at night, with a large grass esplanade stretching eastwards. To the west
            there remains only the abyss, the vast immensity of the aether. Although the wind persists, it seems that it is far from its
            previous violence.",
        cheap_scenery
            1 'aether' "Aether is the substance that flows through the known universe, sustains the flight of floating islands and continents and
                allows navigation between them."
            1 'cliff' "Shaped over many years by the harsh weather, the cliff is at the very edge of the floating island, where the aether begins."
            1 'grass' "The grass rises with vigour as far as your eyes can see.",
        n_to P1L_Cliff,
        s_to P1L_Cliff,
        ne_to P1L_Esplanade,
        e_to P1L_Esplanade,
        se_to P1L_Esplanade,
        cant_go "Just the emptiness of the aether in that direction. It is not possible to go that way.",
	has light;

!-------------------------------------------------    
!----- The esplanade
!-------------------------------------------------

Object P1L_Esplanade "The esplanade"
	with
		description "The grass esplanade opens up around you in all directions, gently rocked by the night wind.",
        cheap_scenery
            1 'aether' "Aether is the substance that flows through the known universe, sustains the flight of floating islands and continents and
                allows navigation between them."
            1 'grass' "The grass rises with vigour as far as your eyes can see.",
        before [;
            Go: if (self hasnt general) {
                    move P1O_Echo1 to Limbo;
                    give self general;
                }
        ],
        nw_to P1L_Cliff,
        w_to P1L_Cliff,
        sw_to P1L_Cliff,
        n_to P1L_Esplanade,
        s_to P1L_Esplanade,
        ne_to P1L_Visitors,
        e_to P1L_Visitors,
        se_to P1L_Visitors,
	has light;
    
Object -> P1O_Echo1 "echo"
    with
        name 'echo',
        describe [;
            "The air seems strangely electrified in this area. You perceive loose images and sounds, which appear and disappear in
            fractions of a second, on the grass. Faces, and voices, that vanish so suddenly that it is not possible to fully comprehend them.
            You know this phenomenon well: It is an ~echo~, a memory of past events that only the empaths like you are able to
            perceive.";
        ],
        description "You examine the echo from a distance. You can vaguely distinguish a man and a woman in changing positions.
            You know that the only way to understand an echo is by touching it, by coming into direct contact with it.",
        before [;
            Touch: move self to Limbo;
                print "You touch the echo with your fingers and instantly experience the well-known sensation of vertigo, while the images and
                sounds flood your mind...
                ^^You find yourself in this same grass esplanade, albeit during the day. To the west you can't see the cliff, but a few low
                hills dotted with trees.
                ^In front of you are a man and a woman, dressed as newlyweds, posing in front of a camera. The photographer, hidden under
                the camera's cloth, gives instructions to the couple.
                ^The two couldn't be more different. She, in her white dress, looks fragile and almost sickly pale, her eyes open like a
                frightened little bird. She caresses her pregnant belly with her thin hands.
                ^The man, on the other hand, is tall and robust, with a prominent chin, abundant moustache and imposing appearance. When he
                surrounds his new bride with his arm, he appears like a black hawk stalking his prey...^^";
                <<Look>>;
        ],
    has static;
    
!-------------------------------------------------
!----- Visitors?
!-------------------------------------------------

Object P1L_Visitors "Visitors?"
	with
		description [;
            print "The esplanade continues endlessly here as well.^";
            if (self.turns_active < 1) "Not far away you can see a figure of extreme slenderness, with its back to you. It seems to contemplate the night sky,
                immersed in deep reflection.";
            "Dozens of ~pale ones~ approach you from all directions except the east.";
        ],
        turns_active,
        before [;
            default:
                if (self.turns_active == 0) {
                    startDaemon(self);
                    rtrue;
                }
        ],
        daemon [;
            switch (++(self.turns_active)) {
                1: print "The figure turns when it hears the crunch of your footsteps and you immediately understand the danger you are in. Its
                    ash-coloured, almost transparent skin and eyes like two bright white beacons reveal that it is a ~pale one~, one of the
                    many spectral beings that roam the Phantom Zone. The clothes he is wearing - now you are able to distinguish them - are
                    faded rags that barely cover his skinny body.
                    ^When he sees you, he opens his mouth and utters a groan similar to an electric sizzle, to which many others are added.
                    From nowhere, dozens of other ~pale ones~ appear and walk towards you from multiple directions. These beings covet the
                    warmth and energy of the living, and now long for yours. If they touch you, they will absorb your vitality and
                    you will become one of them...^^";
                    <<Look>>;
                4: deadflag = 1;
                    if ( location ~= P1L_Visitors && location ~= P1L_Abandoned_House ) print "The ~pale ones~ have entered the house through the door!^";
					"They soon surround you, closing off every possible avenue of escape. You try to defend yourself, but it is useless. Several of their
                    outstretched hands touch your body and pass through your skin, flooding your chest with a deadly cold. Your life escapes from
                    your body like liquid from a spilled bottle...
                    ^Finally, your skin becomes ashen and your eyes light up like headlights. You are now, and will always be, one of them.";
            }
        ],
        cheap_scenery
            1 'aether' "Aether is the substance that flows through the known universe, sustains the flight of floating islands and continents and
                allows navigation between them."
            1 'figure' "There are almost a hundred of them, approaching you from all directions. Even in their skeletal form you can guess
                what they were in another life: Peasants, children, some old people... And each one wants to steal your vital energy!"
            'pale' 'ones' "There are almost a hundred of them, approaching you from all directions. Even in their skeletal form you can guess
                what they were in another life: Peasants, children, some old people... And each one wants to steal your vital energy!"
            1 'grass' "The grass rises with vigour as far as your eyes can see.",
        e_to [;
            self.turns_active = 1;
            return P1L_Abandoned_House;
        ],
        cant_go "You can't go that way... There are too many ~pale ones~ in that direction and they would catch you!",
	has light;
    
!-------------------------------------------------
!----- An abandoned house
!-------------------------------------------------

Object P1L_Abandoned_House "An abandoned house"
	with
		description "The grass esplanade rises slightly to the ruins of an old Victorian-looking house.
            The deteriorated entrance door is open and allows access to the interior of the building.
            ^From all directions around the house, hundreds of ~pale ones~ approach you, squeaking with their electric voices,
            longing for the warmth of your vital essence...",
        cheap_scenery
            1 'aether' "Aether is the substance that flows through the known universe, sustains the flight of floating islands and continents and
                allows navigation between them."
            'pale' 'ones' "There are almost a hundred of them, approaching you from all directions. Even in their skeletal form you can guess
                what they were in another life: Peasants, children, some old people... And each one wants to steal your vital energy!"
            1 'grass' "The grass rises with vigour as far as your eyes can see.",
        in_to [;
            <<Enter P1O_House_door>>;
        ],
        cant_go "You can't go that way... There are too many ~pale ones~ in that direction and they would catch you!",
	has light;

Object -> P1O_House_door "house door"
    with
        name 'abandoned' 'house' 'door',
        description "A Victorian-style home, once the abode of a small well-to-do family, lies in ruins, abandoned to the harsh weather
            of the Phantom Zone. A door leads to its interior.",
        before [;
            Enter: P1L_Visitors.turns_active = 2;
                PlayerTo(P1L_Entrance);
                <<Look>>;
        ],
    has scenery enterable open;
!-------------------------------------------------
!----- Entrance hall
!-------------------------------------------------

Object P1L_Entrance "Entrance hall"
	with
		description "A narrow hall, once small and charming, but long fallen into ruinous decay. A
            splintered and broken old grandfather clock stands dejectedly near the narrow staircase with rotten steps
            that ascends to the first floor. To the north and east, two arches lead to two rooms.",
        cheap_scenery
            'grandfather' 'clock' "After years of exposure to the elements, all that's left of the clock are the mouldy remains of the casing and some rusty pieces of metal. 
                There is nothing useful here."
            'stairs' 'staircase' "The wood of these steps seems on the verge of collapse. Moving up these stairs is an act of courage.",
        n_to P1L_Lower_corridor,
        ne_to P1L_Lower_corridor,
        e_to P1L_Office,
        u_to P1L_Upper_corridor,
	has light;
    
Object -> P1O_Entrance_door "entrance door"
    with
        name 'door',
        describe [;
            if (self hasnt open) "The door remains firmly closed. On the other side are the squeaking voices of the ~pale ones~.";
            "Gusts of wind rattle the open door against the wall.";
        ],
        description [;
            if (self has open) rtrue;
            "The door remains firmly closed. On the other side are the squeaking voices of the ~pale ones~.";
        ],
        before [;
            Close: if (self hasnt open) "It's already closed!";
                stopDaemon(P1L_Visitors);
                give self ~open;
                "You slam the door closed and quickly secure it with a bolt. You retreat a few steps in response to the blows of frustrated
                creatures on the other side... After a while the banging ceases, as if they realised the futility of their efforts.
                ^You're trapped! You must find a way to escape. Maybe if you found a place high enough you could
                signal a passing ship?";
            Open: if (self hasnt open) "Not for all the gold in the world are you going to reopen that door! You need to find another way out...";
                "It is already open!";
        ],
    has static open;
    
!-------------------------------------------------
!----- Lower corridor
!-------------------------------------------------

Object P1L_Lower_corridor "Lower corridor"
	with
		description "The decrepit corridor on the lower floor, with a portrait on the wall as the only ornament, leads to various rooms to
            through exits to the north, east, west, southeast and southwest.",
        cheap_scenery
            1 'portrait' "The portrait, a small oil painting, depicts the husband of the couple who lived in this house. With his
                thick, luxurious moustache, and a bible in his hand, he's shown delivering a passionate sermon to a group of devoted parishioners inside a
                church. Underneath it reads: ~The Words of Reverend Burskil~.",
        s_to P1L_Entrance,
        sw_to P1L_Entrance,
        se_to P1L_Office,
        w_to P1L_Lounge,
        e_to P1L_Kitchen,
	has light;

!-------------------------------------------------
!----- Laundry room
!-------------------------------------------------

Object P1L_Laundry_room "Laundry room"
	with
		description "In the laundry room, the strong odour of stagnant water hits you in the face.  It seems to be coming from the wash basin over in one corner. 
            Towards the south is the exit door.",
	has light;
    
Object -> P1O_Basin "basin"
    with
        name 'basin' 'water',
        description "The stagnant water is so cloudy that it's impossible to discern the bottom of the basin.",
        before [;
            Touch, Take: "Under no circumstances are you going to touch that water!";
        ],
    has scenery enterable,
    
!-------------------------------------------------
!----- Basin bottom
!-------------------------------------------------

Object P1L_Basin_bottom "Basin bottom"
	with
		description "Murky water surrounds you at the bottom of this impossibly deep basin. An invisible force pins you to the muddy
            ground, preventing you from surfacing. Above you, a pallid woman stares at you with her big, unnatural eyes.",
        cheap_scenery
            1 'water' "The water, cloudy and full of dirt, surrounds you completely, like a dark and dense tomb."
            1 'force' "You struggle against the invisible force, but all you do is kick up the mud that surrounds you, making it slip
                into your eyes and blind you momentarily...",
        cant_go "It is as if an unseen power is holding you down against the muddy floor, preventing you from escaping.",
	has light;

!-------------------------------------------------
!----- Kitchen
!-------------------------------------------------

Object P1L_Kitchen "Kitchen"
	with
		description "The kitchen seems to have withstood the passage of time better than other parts of this house. Only the old-fashioned look of
            the stoves and cupboards betray the inexorable passage of time. A door returns you to the corridor to the west.",
        cheap_scenery
            'stove' 'stoves' "The old wood-powered stove still has traces of carbon dust on its surface. There is nothing about it that
                catches your attention."
            'cupboard' 'cupboards' "Not much of use remains in the cupboards, apart from worn and broken old kitchen implements.",
        w_to P1L_Lower_corridor,
	has light;

!-------------------------------------------------
!----- Lounge
!-------------------------------------------------

Object P1L_Lounge "Lounge"
	with
		description "The once comfortable lounge is now dilapidated, with its sofa, moth-eaten chairs, and table seemingly about to
            fall to pieces on the floor. To the east you can go back to the corridor.",
        cheap_scenery
            'chair' 'chairs' "The chairs are so deteriorated that they seem likely to break at the slightest touch."
            1 'table' "The only thing feeding from this table now is termites.",
        w_to P1L_Lower_corridor,
        e_to P1L_Lower_corridor,
	has light;

!-------------------------------------------------
!----- Office
!-------------------------------------------------

Object P1L_Office "Office"
	with
		description "A sober and tiny office, with a leather armchair, a desk and a shelf with a few books. The diploma on the desk displays
            the credentials of the owner of the house. A colourful carpet covers most of the floor here. To the north and west there are exits.",
        cheap_scenery
            'armchair' 'chair' "This chair would have looked elegant in the past, but now its cover is worn and cracked."
            1 'desk' "A wooden desk, sober but elegant. The varnish has almost disappeared, leaving the naked wood exposed."
            1 'diploma' "INTERNATIONAL EVANGELICAL UNIVERSITY
                ^         hereby confers on
                ^           SIMON BURSKIL
                ^       the honorary degree of
                ^DOCTORATE IN THEOLOGY, honoris causa"
            'book' 'books' "~Phenomenology of the Spirit~, by Hegel. An annotated edition of the New Testament. ~Suma teologica~, by Thomas Aquinas.
                There is nothing very useful to you.",
        n_to P1L_Lower_corridor,
        nw_to P1L_Lower_corridor,
        w_to P1L_Entrance,
	has light;

!-------------------------------------------------
!----- Upper corridor
!-------------------------------------------------

Object P1L_Upper_corridor "Upper corridor"
	with
		description "Abundant damp patches plague the upper hallway of the house. One of the two exits to the north has been walled up, more
            quickly than expertly. Another exit to the east leads to a bathroom. It is also possible to descend to the lower floor, and in
            the ceiling a hatch can be seen.",
        cheap_scenery
            'damp' 'patches' "The damp has been penetrating the walls, lifting layers of paint."
            'bricks' 'exits' "The bricks are misaligned and unpainted, but still form a solid, impenetrable barrier.
                 Whoever filled in the doorway was more interested in quickly preventing passage than 
                 attempting to maintain aesthetic decorum."
            'exit' 'wall' "The bricks are misaligned and unpainted, but still form a solid, impenetrable barrier.
                 Whoever filled in the doorway was more interested in quickly preventing passage than 
                 attempting to maintain aesthetic decorum.",
        d_to P1L_Entrance,
        n_to P1L_Bedroom,
        e_to P1L_Bathroom,
	has light;

!-------------------------------------------------
!----- Bedroom
!-------------------------------------------------

Object P1L_Bedroom "Bedroom"
	with
		description "The marital bedroom, with a large bed that was once luxurious but is now just pasture for moths
            and moisture. A window to the north leads to a small balcony, and to the south you can return to the corridor.",
        cheap_scenery
            1 'window' "The window leads outwards to the balcony. To see it better, you can go north.",
        s_to P1L_Upper_corridor,
        n_to P1L_Bedroom_balcony,
	has light;

!-------------------------------------------------
!----- Bedroom balcony
!-------------------------------------------------

Object P1L_Bedroom_balcony "Bedroom balcony"
	with
		description "On the balcony you're assaulted by the icy wind outside. Darkness prevents you from seeing far into the distance, but it is still
            possible to distinguish the figures of the ~pale ones~ ceaselessly circling the building. To your right,
            not far away, is the balcony of another nearby room and the master bedroom lies to the south.",
        cheap_scenery
            'pale' 'ones' "From this distance you cannot distinguish the ~pale ones~ clearly, but they still seem restless, sensing the proximity of
                someone alive..."
            1 'balcony' "There isn't much distance between the balcony you are on and the balcony of the nearby room.",
        s_to P1L_Bedroom,
	has light;

!-------------------------------------------------
!----- A Child's Room
!-------------------------------------------------

Object P1L_Childs_room "A Child's Room"
	with
		description "A strong, stale stench greets you in this room. The crib and the abandoned toys next to the table here, seem to indicate
            that this was a child's room. The door is walled up, so the only way out is via the balcony back to the north.",
        n_to P1L_Abandoned_balcony,
	has light;

!-------------------------------------------------
!----- Abandoned balcony
!-------------------------------------------------

Object P1L_Abandoned_balcony "Abandoned balcony"
	with
		description "This balcony is in a state of even greater disrepair than the adjacent one. The floor beneath your feet seems
            to be shaking worryingly. A broken window leads south inside.",
        cheap_scenery
            1 'window' "A window that was once closed to the elements, but that has been shattered by time. There is nothing left of interest to you.",
        s_to P1L_Childs_room,
	has light;

!-------------------------------------------------
!----- Bathroom
!-------------------------------------------------

Object P1L_Bathroom "Bathroom"
	with
		description "A small washbasin, with its rusty taps, lies sadly abandoned next to the toilet and the bathtub.",
        w_to P1L_Upper_corridor,
	has light;

!-------------------------------------------------
!----- Basement
!-------------------------------------------------

Object P1L_Basement "Basement"
	with
		description "This basement looks like a more informal version of the office on the floor above, with wooden boxes instead of chairs, a
            plank on two easels as a table, and a broken kerosene lamp which once provided lighting.
            The makeshift table features a small shelf. From here you can go up to the upper floor or go east to a storeroom.",
        u_to P1L_Office,
        e_to P1L_Storage_room,
	has light;

!-------------------------------------------------
!----- Storage room
!-------------------------------------------------

Object P1L_Storage_room "Storage room"
	with
		description "A narrow, uncomfortable storeroom plagued with seemingly useless objects. The only exit is to the west.",
        w_to P1L_Basement;

!-------------------------------------------------
!----- Attic
!-------------------------------------------------

Object P1L_Attic "Attic"
	with
		description "This attic is full of broken and dusty furniture covered in cobwebs. Its sloping roof is spanned by colossal wooden beams.
            In the back of the attic, towards the east, you can see some shelves. It is also possible to descend to the first floor."
        e_to P1L_Back_Attic;

!-------------------------------------------------
!----- Back of the Attic
!-------------------------------------------------

Object P1L_Back_Attic "Back of the Attic"
	with
		description "A small improvised room at the back of the attic, delineated by four empty shelves. The only way out is west."
        w_to P1L_Attic;

!-------------------------------------------------
!----- On a beam
!-------------------------------------------------

Object P1L_On_Beam "On a beam"
	with
		description "You are sitting on one of the main beams up in the roof of the attic. A small circular window is set nearby in
            the wall to the north. A shelf unit rests on the beam, allowing you to descend to the attic floor.";

!-------------------------------------------------
!----- Bonfire Tower
!-------------------------------------------------

Object P1L_Bonfire_Tower "Bonfire Tower"
	with
		description "You're on top of a small tower, with an extinguished bonfire in its centre. The remnants of an old tattered rope are tied
            to what remains of the roof. The night sky surrounds you. At some point in time there were steps that
            descended to a lower floor, but they have long since collapsed. The only way out is by crossing the totem pole, which forms an
            improvised bridge leading south to the attic.",
	has light;


 
