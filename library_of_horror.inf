!% -~S

! Library_of_horror.inf - a sample game which uses PunyInform

! Uncomment to add optional features to PunyLib
!Constant DEBUG;
!Constant CUSTOM_ABBREVIATIONS;
!Constant OPTIONAL_FULL_DIRECTIONS;
!Constant OPTIONAL_ALLOW_WRITTEN_NUMBERS;
!Constant OPTIONAL_EXTENDED_VERBSET;
!Constant OPTIONAL_FULL_SCORE;
!Constant OPTIONAL_GUESS_MISSING_NOUN;
!Constant OPTIONAL_MANUAL_SCOPE;
!Constant RUNTIME_ERRORS = 2; ! 0 = Minimal error checking, 1 = Numeric error codes, 2: Error messages

Constant Story      "The Library Of Horror";
Constant Headline   "^A PunyInform demo game^";

Include "globals.h";

! Setup flags
Constant FLAG_COUNT = 8;
Include "ext_flags.h";

! Setup cheap scenery
Include "ext_cheap_scenery.h";

! Setup quote box
Constant QUOTE_V3_SCREEN_WIDTH 40;
Include "ext_quote_box.h";

Include "puny.h";

!===================== Classes, verbs etc for game

! -- testing scope grammar token
! See: https://www.inform-fiction.org/manual/html/s32.html 

[ QueryTopic;
  switch (scope_stage) {
      1: rfalse;
      2: ScopeWithin(WhatIsTopics); rtrue;
      3: "You haven't the faintest.";
  }
];

[ QuerySub; noun.description();];

Verb 'what' * 'is'/'was' scope=QueryTopic -> Query;

Class MultiTopic
	with
		number 0,
		parse_name [ _w1 _w2 _i _sw1 _sw2 _len;
			_w1 = NextWordStopped();
			_w2 = NextWordStopped();
			_i = 0;
			_len = self.#topics / 2;
#IfTrue RUNTIME_ERRORS > RTE_MINIMUM;
#IfTrue RUNTIME_ERRORS == RTE_VERBOSE;
			if(_len % 3 > 0)
				"ERROR: multi_topic topics property of ", (the) self," has incorrect # of values!^";
#IfNot;
			if(_len % 3 > 0)
				"ERROR: multi_topic #1!^";
#EndIf;
			while(_i < _len) {
				_sw1 = self.&topics-->(_i+2);
#IfTrue RUNTIME_ERRORS == RTE_VERBOSE;
				if(~~(_sw1 ofclass String or Routine))
					"ERROR: Element ", _i+2, " in multi_topic topics property of ", (the) self," is not a string or routine!^",
						"Element: ", (name) _sw1, "^";
#IfNot;
				if(~~(_sw1 ofclass String or Routine))
					"ERROR: multi_topic #2!^";
#EndIf;

				_i = _i + 3;
			}
			_i = 0;
#endif;
			while(_i < _len) {
				_sw1 = self.&topics-->_i;
				_sw2 = self.&topics-->(_i+1);
				if(_w1 == _sw1 && _w2 == _sw2) {
					self.number = _i;
					return 2;
				}
				if(_w1 == _sw1 or _sw2) {
					self.number = _i;
					return 1;
				}
				_i = _i + 3;
			}
			! It would make sense to return 0 here, but property
			! routines return 0 by default anyway.
		],
		description [ _k;
			_k = self.&topics-->(self.number + 2);
			if(_k ofclass Routine) {
				_k();
				rtrue;
			}
			print_ret (string) _k;
		];

Object WhatIsTopics "WhatIsTopics";

MultiTopic -> MultiQuestion_1 "MultiQuestion_1"
	with
		topics
			1 'library' "A library is venue where books are kept and people can borrow said books."
			1 'grue' "A grue is a sinister creature lurking in the dark. If anyone has actually seen a grue,
				they have not lived long enough to tell the tale."
			'library' 'manager' "Library managers oversee the daily activities and functions of a library.";  

Array quote_1 --> 5 35
"When I die, I want to go peacefully"
"in my sleep like my grandfather."
"Not screaming in terror, like the" 
"passengers in his car."
"               -- Jack Handey";

Object Library "The Library"
	with
		description "You are in a library. The stairs leading down to the basement are to the east.",
		cheap_scenery
			1 'library' "It's a beautiful old library.",
		e_to TopOfStairs,
	has light;

Object -> Manager "library manager"
	with
		article "the",
		parse_name [ _w1;
			_w1 = NextWordStopped();
			if(_w1 == 'manager') return 1;
			if(_w1 == 'library' && NextWordStopped() == 'manager') return 2;
		],
	has animate;

Object TopOfStairs "Staircase Leading Down"
	with
		description "The stairs descend into a forboding darkness. The main hall of the library is off to the west.",
		w_to Library,
		d_to BottomOfStairs,
		after [;
			if(self hasnt general) {
				give self general;
				QuoteBox(quote_1);
			}
		],
	has light;
		
Object BottomOfStairs "Basement, Near Staircase"
	with
		description "It's nearly dark here, since the lamp in the ceiling is missing its light bulb.
			However, there's enough light coming in from the stairs and nearby rooms to make
			out your surroundings. There is more light to the east.",
		u_to TopOfStairs,
		e_to StorageRoom,
	has light;

! Data that Frank needs to print where he came from.	
#IfDef OPTION_FULL_DIRECTIONS;
Array opposite_directions table "south" "north" "west" "east" "southwest" "southeast" "northwest" "northeast" "down" "up" "out" "in";
#IfNot;
Array opposite_directions table "south" "north" "west" "east" "down" "up" "out" "in";
#EndIf;

Object -> Frank "Frank the Robot"
	with
		name 'frank' 'the' 'robot',
		when_off [;
			give self on;
			give self moved;
			StartDaemon(self);
			"Frank the Robot is standing here, all dark and quiet. As you enter the room, some LEDs light up and Frank's eyes start to glow with a feeble yellow light.";
		],
		before [;
			SwitchOff:
				"A bright light arc juts out from near the button and the electric shock makes you jump back.";
		],
		daemon [ _my_loc _i _dir_prop _dirs ;
			! Frank wants to follow the player around, but can't go up/down/in/out
			_my_loc = parent(self);
			if(location ~= _my_loc){
				_dirs = direction_properties_array-->0;
				for(_i=1 : _i <= _dirs : _i++) {
					_dir_prop = direction_properties_array-->_i;
					if(_dir_prop ~= u_to or d_to or in_to or out_to &&
						_my_loc provides _dir_prop && _my_loc._dir_prop == location) {
						move self to location;
						"^Frank enters from the ", (string) opposite_directions-->_i, ".";
					}
				}
			}
		],
		description "Frank has a rather dreary look.",
	has animate proper switchable;
	
Object StorageRoom "Storage Room"
	with 
		description "A small storage room.",
		w_to BottomOfStairs,
	has light;
	

	
[Initialise;
	location = Library;
	print "^^And so the story begins...^^";
];
