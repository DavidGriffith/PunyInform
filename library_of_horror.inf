!% -~S

! Library_of_horror.inf - a sample game which uses PunyInform

! Uncomment to add optional features to PunyLib
!Constant CUSTOM_ABBREVIATIONS;
!Constant OPTIONAL_ALLOW_WRITTEN_NUMBERS;
!Constant OPTIONAL_DEBUG_VERBS;
!Constant OPTIONAL_EXTENDED_VERBSET;
!Constant OPTIONAL_FULL_SCORE;
!Constant OPTIONAL_GUESS_MISSING_NOUN;

Constant Story      "The Library Of Horror";
Constant Headline   "^A PunyInform demo game^";

Include "globals.h";

! Setup flags
Constant FLAG_COUNT = 8;
Include "ext_flags.h";

! Setup cheap scenery
Include "ext_cheap_scenery.h";

! Setup quote box
Constant QUOTE_V3_SCREEN_WIDTH 40;
Include "ext_quote_box.h";

Include "puny.h";

Array quote_1 --> 5 35
"When I die, I want to go peacefully"
"in my sleep like my grandfather."
"Not screaming in terror, like the" 
"passengers in his car."
"               -- Jack Handey";

Object Library "The Library"
	with
		description "You are in a library.",
		cheap_scenery
			1 'library' "It's a beautiful old library.",
		after [;
			if(self hasnt general) {
				give self general;
				QuoteBox(quote_1);
			}
		],
		e_to TopOfStairs,
	has light;

Object TopOfStairs "Staircase Leading Down"
	with
		description "The stairs descend into a forboding darkness.",
		w_to Library,
		d_to BottomOfStairs,
	has light;
		
Object BottomOfStairs "Basement, Near Staircase"
	with
		description "It's nearly dark here, since the lamp in the ceiling is missing its light bulb.
			However, there's enough light coming in from the stairs and surrounding rooms to make
			out your surroundings.",
		u_to TopOfStairs,
	has light;
	
Object -> Frank "Frank the Robot"
	with
		name 'frank' 'the' 'robot',
		when_on [;
			give self moved;
			"Frank the Robot is standing here, all dark and quiet. As you enter the room, some LEDs light up and Frank's eyes start to glow with a feeble yellow light.";
		],
		before [;
			SwitchOff:
				"An bright light arc juts out from near the button and the electric shock makes you jump back.";
		],
		description "Frank has a rather dreary look.",
	has animate proper switchable on;
	
[Initialise;
	location = Library;
	print "^^And so the story begins...^^";
];
